package grange

type rangeQuery Peg {
  currentLiteral string
  nodeStack []parserNode
}

expression <- combinedexpr? !.

combinedexpr <- rangeexpr combinators?

rangeexpr <- space
    ( const
    / function
    / cluster
    / group
    / groupq
    / localkey
    / regex
    / value
    / brackets
    / { p.addBraceStart() } braces
    )
    space

combinators <- space (union / intersect / exclude / braces)
intersect   <- '&' rangeexpr  { p.addOperator(operatorIntersect) } combinators?
exclude     <- '-' rangeexpr  { p.addOperator(operatorSubtract) } combinators?
union       <- ',' rangeexpr  { p.addOperator(operatorUnion) } combinators?

# See https://github.com/pointlander/peg/issues/21 for context
braces   <- '{' combinedexpr? '}' rangeexpr? { p.addBraces() }
brackets <- '(' combinedexpr? ')'

groupq  <- '?' rangeexpr { p.addGroupQuery() }
cluster <-  '%' literal { p.addValue(buffer[begin:end]); p.addClusterLookup() } key?
          / '%' rangeexpr { p.addClusterLookup() } key?
group   <- '@' rangeexpr { p.addGroupLookup() }

# TODO: Use rangeexpr for the following?
key      <- ':' rangeexpr { p.addKeyLookup() }
localkey <- '$' literal { p.addLocalClusterLookup(buffer[begin:end]) }

function <- literal { p.addFunction(buffer[begin:end]) } '(' funcargs ')'
funcargs <- combinedexpr? { p.addFuncArg() } ';' funcargs
          / combinedexpr? { p.addFuncArg() }

regex      <- '/' < (!'/' .)* > '/' { p.addRegex(buffer[begin:end]) }
literal    <- < leaderChar [[a-z0-9-_]]* >
value      <- < leaderChar [[:a-z0-9-_.]]* > { p.addValue(buffer[begin:end]) }
leaderChar <- [[a-z0-9._]] # Do not match "-" so not to confuse with exclude rule
space      <- ' '*
const      <- q / quoted
q          <- 'q(' <(!')' .)*> ')' { p.addConstant(buffer[begin:end]) }
quoted     <- '"' <(!'"' .)*> '"' { p.addConstant(buffer[begin:end]) }
